#!/bin/csh
###########################################################
#   Initialize LSF queue parameters
#
# "-q" gives the queue (1nh by default)
# "-c" gives the CPU time in minutes
#
# -q 8nm  = about 30 min HP735     express
# -q 1nh           3 hours         short
# -q 8nh           1 day           medium
# -q 1nd           3 days          long
# -q 1nw          10 days HP K260  verylong
#
#
#  define the job name
#BSUB -J myjob
#  define the queue
#BSUB -q 8nh
#
###########################################################
#
# in BATH output files are written in the BATCH machine
# and copy at the end to your $MYWORKDIR output directory
# in INTERACTIVE output files are written onto $WORKDIR
# you have to set $WORKDIR to what you want
#
if ($#argv == 0 ) then
  echo "\nUsage: DaVinci.job <dbase-version> [<mode>]"
  echo " where:"
  echo "  <dbase-version> (mandatory) is SICB/dbase version (e.g. v241)"
  echo "  <mode>          (optional)  is execution mode (e.g. rh61_gcc2952dbx)"
  exit 0
else
  echo "using SICB/dbase version $1"
  setenv LHCBDBASE $LHCBSOFT/SICB/dbase/$1
endif
#
set MODE = $CMTCONFIG
if ($?LS_SUBCWD) then 
# in BATCH go to the BATCH local directory
  cd $WORKDIR
else
# in interactive set WORKDIR to your output directory
  setenv WORKDIR $HOME/w1
  if ($#argv == 2) set MODE = "$2" 
endif
set current = $PWD
#
if ($?LS_SUBCWD) then 
# in BATCH, location of executable has to be given explicitly
  setenv MYJOBROOT $HOME/newmycmt/Phys/DaVinci/v4r0
else
# in interactive, executable is under same root directory as this job
  cd `dirname $0` 
  cd ..
  set MYJOBROOT = $PWD
endif

# == set the program environment
source $MYJOBROOT/cmt/setup.csh -tag=$MODE

# == if not linked in Static mode set soft_links to use packages
#if ($MODE != "LinuxStatic") then
#  cd $MYJOBROOT/cmt
#  $CMTROOT/mgr/cmt build library_links
#  cd $current
#endif

# == set executable name
setenv EXEC DaVinci

# == in batch copy the executable,======================================= 
#    in interactive create a link to the executable you want to run =======
#
if ($?LS_SUBCWD) then
  cp $MYJOBROOT/$MODE/$EXEC.exe tmpjob.exe
else
  ln -fs $MYJOBROOT/$MODE/$EXEC.exe tmpjob.exe
endif
#
#==  create temporary joboptions file ==================
#
#
cp $MYJOBROOT/options/DaVinci.opts tmpjob.opts

#
# == run $EXEC.exe ============================================
#
if ($MODE == "rh61_gcc2952dbx") then
  setenv JOBOPTPATH tmpjob.opts
  gvd tmpjob.exe
else
  time tmpjob.exe tmpjob.opts
endif

# == job is finished: remove temporary files ==========
rm tmp*

# == job is finished: if in batch copy output files on user directories
#
if ($?LS_SUBCWD) then 
  ls *.* >! RETURN
endif

exit 0











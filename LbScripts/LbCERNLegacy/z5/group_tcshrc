if ! ( -e ${HOME}/.nogrouplogin || -e ${HOME}/.noLHCBLoginscript ) then


  #
  # Setting the default config to slc5-gcc46
  #
  if ( ! $?CMTCONFIG ) then
  setenv CMTCONFIG x86_64-slc5-gcc46-opt 
  endif

if ( ! -e ${HOME}/.oldLHCBLoginscript ) then
  # Post setup, CMTCONFIG specific (compilers setup for example)
  setenv LOGIN_POST_SCRIPT ${GROUP_DIR}/post/login
  # Post setup, site wide, does not depend on CMTCONFIG (X509 vars for example)
  setenv LHCB_POST_SCRIPT ${GROUP_DIR}/post/site
  set lbvers=prod
  if ( -e ${HOME}/.prevLHCBLoginscript ) then
    set lbvers=prev
  endif
  if ( -e ${HOME}/.devLHCBLoginscript ) then
    set lbvers=dev
  endif
  set use_cvmfs=0
  if ( -e ${HOME}/.devLHCBuseCVMFS && -e /cvmfs/lhcb.cern.ch/lib/lhcb/LBSCRIPTS/prod/InstallArea/scripts/LbLogin.csh ) then
    set use_cvmfs=1
  endif
  if (  ($?ENVIRONMENT) ) then
  if ( ${ENVIRONMENT} == "BATCH" && ( !  -e ${HOME}/.devLHCBuseAFS ) && -e /cvmfs/lhcb.cern.ch/lib/lhcb/LBSCRIPTS/prod/InstallArea/scripts/LbLogin.csh ) then
    set use_cvmfs=1
  endif
  endif

  if ( $?LBLOGIN_DONE ) then
    if ( $use_cvmfs == 1 ) then
     source  /cvmfs/lhcb.cern.ch/lib/lhcb/LBSCRIPTS/prod/InstallArea/scripts/LbLogin.csh  --silent >& /dev/null
   else
     source /afs/cern.ch/lhcb/software/releases/LBSCRIPTS/$lbvers/InstallArea/scripts/LbLogin.csh --silent >& /dev/null 
   endif
  else
     setenv LB_BANNER `mktemp -t LbLogin_banner.XXXXXXXX`
     if ( $use_cvmfs == 1 ) then
       source  /cvmfs/lhcb.cern.ch/lib/lhcb/LBSCRIPTS/prod/InstallArea/scripts/LbLogin.csh  --quiet >! ${LB_BANNER}
     else
       source /afs/cern.ch/lhcb/software/releases/LBSCRIPTS/$lbvers/InstallArea/scripts/LbLogin.csh --quiet >! ${LB_BANNER}
     endif
  endif
  unset lbvers
endif 

if ( ! $?LHCB_POST_DONE ) then
  if ( $?LHCB_POST_SCRIPT ) then
    if ( -r $LHCB_POST_SCRIPT.csh ) then
      source $LHCB_POST_SCRIPT.csh
      setenv LHCB_POST_DONE yes
    endif
  endif
endif


endif

source ${GROUP_DIR}/extras.csh
